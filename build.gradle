
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}

apply plugin: 'android'
apply plugin: 'eclipse'

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.1'

    dependencies {
        compile fileTree(dir: 'libs', include: '*.jar')
        compile 'com.google.android.gms:play-services:4.0.30'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    signingConfigs {
      release
    }

    buildTypes {
        debug {
            jniDebugBuild true
        }

        release {
            debuggable true
            runProguard true
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError true
        disable 'MissingTranslation'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = ['-Xlint:all', '-Werror', '-Xlint:-deprecation']
}

if (hasProperty('StoreFile')) {
    android.signingConfigs.release.storeFile = file(StoreFile)
    android.signingConfigs.release.storePassword = StorePassword
    android.signingConfigs.release.keyAlias = KeyAlias
    android.signingConfigs.release.keyPassword = KeyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

// Generate AndroidManifest.xml
String mozillaAPIKey = hasProperty('MozAPIKey') ? MozAPIKey : "FAKE_MOZILLA_API_KEY";
String mapAPIKey     = hasProperty('MapAPIKey') ? MapAPIKey : "FAKE_MAP_API_KEY";
String tileServerURL = hasProperty('TileServerURL') ? TileServerURL : "http://tile.openstreetmap.org/";

File manifest = file("./AndroidManifest.xml.in")
String content = manifest.getText('UTF-8')
content = content.replaceAll(/MY_MOZILLA_API_KEY/, mozillaAPIKey)

content = content.replaceAll(/MY_MAP_API_KEY/, mapAPIKey)
content = content.replaceAll(/MY_TILE_SERVER/, tileServerURL)

manifest = file("./AndroidManifest.xml")
manifest.write(content, 'UTF-8')

